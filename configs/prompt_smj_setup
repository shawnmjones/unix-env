#
# A theme based on Steve Losh's Extravagant Prompt with vcs_info integration.
#
# Authors: Shawn M. Jones <smj@littleprojects.org>
#

HOSTSHORT=`hostname -s`
HOSTFULL=`hostname -f`

function prompt_steeef_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="(%s:%b:%u:%c:%m *)"
  else
    branch_format="(%s:%b:%u:%c:%m)"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  echo -ne "\e]1;${USER}@${HOSTSHORT}:${PWD/#$HOME/~}\a"

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_steeef_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_steeef_precmd

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="(%b%u%c)"
  local action_format="(%a)"
  local unstaged_format="unstaged"
  local staged_format="staged"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
%B%*%b %S%n@%m:%~%s 
zsh (%h) "'${vcs_info_msg_0_}'" %# --> "
  RPROMPT=''
#"'$python_info[virtualenv]'"
}

prompt_steeef_setup "$@"
